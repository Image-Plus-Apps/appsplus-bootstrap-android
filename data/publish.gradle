apply plugin: 'maven-publish'

def LIB_GROUP_ID = 'uk.co.appsplus.bootstrap'
def LIB_ARTIFACT_ID = 'data'
def LIB_VERSION = project.hasProperty('version') ? project.property('version') : "0.0.1"

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "sources"
}

publishing {
    def envFile = rootProject.file(".env")
    def envProperties = new Properties()
    if (envFile.exists()) {
        envProperties.load(new FileInputStream(envFile))
    }
    repositories {
        maven {
            name = "GithubPackages"
            url = uri("https://maven.pkg.github.com/richard-clements/appsplus-bootstrap-android")
            credentials {
                username = envProperties['GITHUB_USER'] ?: project.properties['GITHUB_USER']
                password = envProperties['GITHUB_PERSONAL_ACCESS_TOKEN'] ?: project.properties['GITHUB_PERSONAL_ACCESS_TOKEN']
            }
        }
        maven {
            name = 'CustomMavenRepo'
            url = "file://${buildDir}/repo"
        }
    }
    publications {
        data(MavenPublication) {
            groupId LIB_GROUP_ID
            artifactId LIB_ARTIFACT_ID
            version LIB_VERSION
            artifact("$buildDir/outputs/aar/data-release.aar")
            artifact(sourceJar)

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                //Iterate over the compile dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.api.allDependencies.each {
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', it.group)
                    dependencyNode.appendNode('artifactId', it.name)
                    dependencyNode.appendNode('version', it.version)
                }
            }
        }
    }
}