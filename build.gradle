buildscript {
    ext.kotlin_version = '1.5.31'
    ext.detekt_version = "1.16.0"
    ext.kotest_version = "5.4.2"
    repositories {
        google()
        mavenCentral()
        maven { url "https://jitpack.io" }
        mavenLocal()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.0.4'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "io.gitlab.arturbosch.detekt:detekt-gradle-plugin:$detekt_version"
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    def envFile = rootProject.file(".env")
    def envProperties = new Properties()
    if (envFile.exists()) {
        envProperties.load(new FileInputStream(envFile))
    }
    def projectVersion = project.hasProperty('version')
            && project.property('version') != 'unspecified' ? project.property('version') : "1"
    ext {
        coroutines_version = "1.6.3"
        lifecycle_version = "1.3.2"
        sdk_version = projectVersion
    }
    repositories {
        google()
        mavenLocal()
        mavenCentral()
        maven { url "https://jitpack.io" }
        maven {
            name = "GithubPackages"
            url = uri("https://maven.pkg.github.com/richard-clements/appsplus-bootstrap-android")
            credentials {
                username = envProperties['GITHUB_USER'] ?: project.properties['GITHUB_USER']
                password = envProperties['GITHUB_PERSONAL_ACCESS_TOKEN'] ?: project.properties['GITHUB_PERSONAL_ACCESS_TOKEN']
            }
        }
    }
    apply from: "$rootDir/detekt.gradle"
}
task buildAll(type: Exec) {
    commandLine './build.sh'
}
task clean(type: Delete) {
    delete rootProject.buildDir
}